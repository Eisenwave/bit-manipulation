document
    = { content };
content
    = directive | text;

(* not matched if immediately following "\", except if following "\\" *)
directive
    = "\", directive_name, [ arguments ], [ block ];
directive_name
    = /[-a-zA-Z][-a-zA-Z0-9]*/;

argument_list
    = "[", [ argument ], { ",", [ argument ] }, "]";
argument
    = [ ws ], argument_name, [ ws ], "=", [ ws ], [ argument_value ], [ ws ]
    | [ ws ], [ argument_value ], [ ws ];
argument_name
    = /[-_a-zA-Z][-_a-zA-Z0-9]*/;

(* terminated by "," or unbalanced "]" *)
argument_value
    = { content };

(* Lazy.
   Brace matching is performed,
   where escaped braces ("\}" and "\{") do not count,
   except that "\\{" and "\\}" do count  *)
block
    = "{", { content }, "}";

text
    = (* sequence of characters not containing any match of directive *);
ws
    = (* sequence of whitespace characters *);
